#! /usr/bin/perl
my $id = $0 =~ s{.*/}{}r;
my $script = sprintf qq{#line %d "%s"\n%s;1}, __LINE__ + 1, __FILE__, <<'__EOT__'; eval $script or die $@;
use strict;
my $SSH = 'ssh';
my $PERL = 'perl';
my $SYSFS = '/sys';
my $modprobe = 'modprobe';
my $drivers = "$SYSFS/bus/usb/drivers";
my $devices = "$SYSFS/bus/usb/devices";
my ($verbose);
eval qq{\$$1="\Q$2\E";shift} or die $@ while $ARGV[0] =~ /^(\w+)=(.*)/s;
my $logger = sub { shift; print STDERR @_ };
use constant {
	LOG_WARNING => 4, LOG_ERR => 3, LOG_INFO => 6, LOG_DEBUG => 7,
	LOG_DAEMON => 3<<3,
};
sub DEBUG {} *DEBUG = sub { $logger->(LOG_DEBUG, @_) } if $verbose;
$SIG{__DIE__} = sub { die @_ if $^S; $logger->(LOG_ERR, "ERROR: @_"); exit 1 };
$SIG{__WARN__} = sub { $logger->(LOG_WARNING, "WARNING: @_") };
my $op = shift;
if($op eq 'remote'){
	remote_script(@ARGV);
}elsif($op eq 'list'){
	list_devices(@ARGV);
}elsif($op eq 'unbind'){
	remote_detach($_) for find_dev(mk_dev_pattern(@ARGV), 1);
}elsif($op eq 'detach'){
	local_detach(@ARGV);
}elsif($op eq 'keep'){
	persistent(@ARGV);
}elsif($op eq 'daemon'){
	daemon(@ARGV); 
}elsif(length $op){
	exit xwait(local_script($op, @ARGV));
}else{
	print <<"EOT"; exit 1;
usage:
	$id USER\@HOST
	$id USER\@HOST list [DEV_PATTERN]
		list usb devices from HOST

	$id USER\@HOST DEV_PATTERN
		attach single device matching DEV_PATTERN from HOST

	$id keep USER\@HOST DEV_PATTERN
		same command as above, but keep trying to connect to HOST
		and to reconnect to it if the connection is broken

	$id daemon USER\@HOST DEV_PATTERN
		same as 'keep' but detached from the tty and using syslog
		for messages and errors

	$id list [DEV_PATTERN]
		list usb devices from the local machine

DEV_PATTERN is as returned by the 'list' command: a busid like 3-3.1, a
vip:pid like 03f0:e111, or a pattern matching the vid:pid, the product name
or the serial number.

You can add command line options for ssh before USER\@HOST.
EOT
}

###############################################

sub readfile { open my $h, shift or return; local $/; <$h> =~ s/\s+$//r }
sub read_uevent { split /[=\n]/, readfile shift }
sub deb { DEBUG "@_\n" }
sub xwritefile {
	my $f = shift;
	open my $h, '>', $f or die "open> $f: $!\n";
	syswrite $h, "@_" or die "write $f < @_: $!\n";
	deb "WRITE $f < @_";
}
sub xreadfile {
	my $f = shift;
	open my $h, '<', $f or die "open< $f: $!\n";
	local $/; my $d = <$h> or die "readline< $f: $!\n";
	$d =~ s/\s+$//;
	die "\nempty file $f" unless length $d;
	deb "READ $f > $d";
	$d;
}
sub xsystem {
	system(@_) == -1 and die "system $_[0]: $!";
	die "system @_: status ".($? >> 8) if $?;
	deb "EXEC @_";
}
sub xtmpdir {
	require File::Temp;
	File::Temp->newdir("$id-XXXXXX", TMPDIR => 1);
}
sub xeval(&) {
	eval { shift->(@_) }; $logger->(LOG_ERR, $@) if $@;
}
sub xopen(\[$*]$) {
	my ($h, $f) = @_; open $$h, $f or die "open $f: $!";
}

###############################################

use constant {
	LOW => 1, FULL => 2, HIGH => 3, SUPER => 5, SUPER_PLUS => 6,
	SDEV_ST_AVAILABLE => 1, SDEV_ST_USED => 2,
	VDEV_ST_NULL => 4, VDEV_ST_USED => 6,
};
sub remote_attach {
	my ($sockfd, $busid, $unmount) = @_;
	die "$busid is a hub, and usbip-host cannot attach to a hub\n"
		if readfile("$devices/$busid/bDeviceClass") eq '09';
	my $status = readfile "$drivers/usbip-host/$busid/usbip_status";
	if($status == SDEV_ST_USED){
		xwritefile "$devices/$busid/usbip_sockfd", -1;
	}elsif($status != SDEV_ST_AVAILABLE){
		xsystem $modprobe, 'usbip-host'
			unless -d "$drivers/usbip-host";
		if(-l "$devices/$busid/driver"){
			do_unmounts($busid) if $unmount;
			xwritefile "$devices/$busid/driver/unbind", $busid;
		}
		xwritefile "$drivers/usbip-host/match_busid", "add $busid";
		xwritefile "$drivers/usbip-host/bind", $busid;
	}
	xwritefile "$devices/$busid/usbip_sockfd", $sockfd;
}
sub remote_detach {
	my $busid = shift;
	xwritefile "$drivers/usbip-host/unbind", $busid;
	xwritefile "$drivers/usbip-host/match_busid", "del $busid";
	xwritefile "$drivers/usbip-host/rebind", $busid;
}
sub local_attach {
	my ($sockfd, $bus, $dev, $speed) = @_;
	# as per linux/usb/ch9.h and drivers/usb/core/sysfs.c
	my %hspeed = ( 1.5 => LOW, 12 => FULL, 480 => HIGH,
		5000 => SUPER, 10000 => SUPER_PLUS, 20000 => SUPER_PLUS,
	);
	my $hspeed = $hspeed{$speed} // HIGH;
	my ($vhci, $port) = find_vhci_and_port($hspeed);
	xwritefile "$vhci/attach",
		sprintf "%d %d %d %d", $port, $sockfd, $bus<<16|$dev, $hspeed;
}
sub local_detach {
	my (@b, $n); my %b = map {$_ => 1} @_;
	for my $f (<$SYSFS/devices/platform/vhci_hcd.*/status*>){
		open my $h, '<', $f or warn("open $f: $!"), next;
		<$h>; # skip header
		while(<$h>){
			my ($hub, $port, $sta, $spd, $dev, $sockfd, $busid)
				= split;
			next unless $sta == VDEV_ST_USED;
			push @b, $busid;
			next unless $b{all} or $b{$busid};
			my ($vhci) = $f =~ m{(.*)/};
			xwritefile "$vhci/detach", int($port);
			$n++;
		}
	}
	die "no devices attached\n" unless @b;
	die "'@_' did not match any of: @b\n" unless $n;
}
###############################################

use Socket qw(AF_UNIX SOCK_STREAM SOCK_DGRAM MSG_DONTWAIT pack_sockaddr_un);
sub do_unmounts {
	my ($busid) = @_;
	my (%dev, @dir);
	find("$devices/$busid", "dev", sub { $dev{readfile shift} = 1 });
	return unless keys %dev;
	xopen my $h, '</proc/self/mountinfo';
	for(reverse <$h>){
		my @d = split;
		if(exists $dev{$d[2]}){
			deb "@d[-2, 4, 2, -3]";
			push @dir, $d[4] =~ s/\\(...)/chr oct $1/rge;
		}
	}
	xsystem qw(umount --), @dir if @dir;
}
sub find_vhci_and_port {
	my $speed = shift;
	my $shub = $speed >= SUPER ? 'ss' : 'hs';
	xsystem $modprobe, 'vhci-hcd'
		unless <$SYSFS/devices/platform/vhci_hcd.*>;
	for my $f (<$SYSFS/devices/platform/vhci_hcd.*/status*>){
		open my $h, '<', $f or warn "open $f: $!" and next;
		<$h>; # skip header
		while(<$h>){
			my ($hub, $port, $sta) = split;
			return $f =~ m{^(.*)/}, int($port)
				if $sta == VDEV_ST_NULL and $hub eq $shub;
		}
	}
	die "no suitable vhci port found for speed = $speed";
}
sub mk_if_spec {
	sprintf '%02x%02x%02x', split '/', shift
}
sub mk_dev_spec {
	my ($p, %e) = @_;
	my @extra = map readfile("$p/$_"), qw(serial manufacturer product);
	my ($id) = sprintf '%04x:%04x', map hex, split '/', $e{PRODUCT};
	my $busid = $p =~ s{.*/}{}r;
	my $ifs = join ' ', map mk_if_spec(${{read_uevent($_)}}{INTERFACE}),
		<$p/$busid:*/uevent>;
	"$busid  $id  $e{BUSNUM}/$e{DEVNUM}  @extra\t$ifs"
}
sub mk_dev_pattern {
	for(my $p = shift){
		my $n = s/^[!-]//;
		s/^(\d+-\d+(?:\.\d+)*)$/^\Q$1\E /;
		s/\b0*([0-9a-f]+)(?=:)|:\K0*([0-9a-f]+)\b/\\b0*\L$1$2\\b/gi;
		s/\s+/ +/g;
		return $n ? qr/^(?!.*$_)/s : qr/$_/;
	}
}
sub find_dev {
	my ($pat, $min, $single) = @_;
	deb "looking for $pat inside $devices";
	my @found;
	for my $p (<$devices/[1-9]*>){
		next unless my ($busid) = $p =~ m{.*/([1-9][\d.-]+)$};
		my $spec = mk_dev_spec($p, read_uevent "$p/uevent");
		next if $spec =~ /\b090000\b/; # XXX a hub
		push @found, $busid if my $found = $spec =~ $pat;
		deb sprintf "  %2s %s", $found && "=>", $spec;
	}
	if($single){ return $found[0] if @found == 1 }
	else{ return @found if @found >= $min }
	die @found ? "multiple" : "no", " devices match '$pat'\n";
}
sub xwait {
	my ($pid) = @_;
	while(waitpid($pid, 0) == -1){ die "waitpid $pid: $!" unless $!{EINTR} }
	$? ? ($? >> 8 or $? | 64) : 0
}
sub ssh_myself {
	use IPC::Open3;
	my ($stdout, $stderr, $ssh_perl, @args) = @_;
	unshift @args, 'verbose=1' if $verbose;
	/[^\w.-]/ and s/'/'\\''/g, $_ = "'$_'" for @args;
	deb "EXEC @$ssh_perl - @args";
	my $pid = open3 my $to, $_[0], $_[1], @$ssh_perl, '-', @args;
	syswrite $to, join "\n",
		qq{my \$id = "\Q$id\E"; \$0 = "\Q$id [remote] @args\E";},
		q{my $script = <<'__EOT__'; eval $script or die $@;},
		$script, '__EOT__', '__END__'
			or die "syswrite: $!";
	deb "pid=$pid";
	return $pid;
}
sub ssh_myself_simple {
	ssh_myself '>&STDOUT', '>&STDERR', @_;
}
sub copy_output {
	my $o = shift =~ s/^((ERROR:)|(WARNING:)|)/    /r;
	return unless $1 or $verbose;
	$logger->($2 ? LOG_ERR : $3 ? LOG_WARNING : LOG_DEBUG, $o);
}
sub mk_unix_socket {
	my ($spec, $path) = @_;
	my $type = $spec =~ /d/ ? SOCK_DGRAM : SOCK_STREAM;
	socket my $sock, AF_UNIX, $type, 0 or die "socket AF_UNIX, $type: $!";
	my $a = pack_sockaddr_un $path;
	if($spec =~ /l/){
		bind $sock, $a or die "bind to $path: $!";
		listen $sock, 12 or die "listen on $path: $!";
	}else{
		connect $sock, $a or die "connect to $path: $!";
	}
	$sock
}
# with 'my $p = defer sub { ... }', 'sub' will be called when '$p' goes
# out of scope
sub defer {
	package defer; sub DESTROY { my $s = shift; $$s->() if $$s }
	bless \shift
}
sub local_script {
	my @ssh = $SSH;
	while($_[0] =~ /^-/){
		push @ssh, my $o = shift;
		# see getopt(..) in ssh.c
		push @ssh, shift
			if $o =~ /^-[1246afgknqstvxACGKMNPTVXYy]*[bceilmopBDEFIJLOQRSwW]$/;
	}
	my $uhost = shift;
	unless(@_){
		return ssh_myself_simple [@ssh, $uhost, $PERL], 'list';
	}elsif($_[0] =~ /^(?:list|find)$/ or $_[0] eq '--' and shift){
		return ssh_myself_simple [@ssh, $uhost, $PERL], @_;
	}
	my $tmpdir = xtmpdir;
	my @ssh_S = ($ssh[0], -S => "$tmpdir/ctl", $uhost);
	my $pid = ssh_myself my $sfrom, undef,
		[@ssh, -S => "$tmpdir/ctl", '-M', $uhost, $PERL], 'remote', @_;
	my $defer = defer sub { deb "kill $pid"; kill 'TERM', $pid };
	my $rpath;
	while(<$sfrom>){
		copy_output($_);
		if(/^-Socket (.*)/){ $rpath = $1; last }
	}
	die "ssh/remote failure\n" unless $rpath;
	my $lpath = "$tmpdir/vhci";
	deb "listen on $lpath";
	my $lsock = mk_unix_socket(l => $lpath);
	xsystem @ssh_S, qw(-O forward), -R => "$rpath:$lpath";
	xsystem @ssh_S, qw(-q -O stop);
	while(<$sfrom>){
		copy_output($_);
		if(/^-Dev (.*)/){
			my ($bus, $dev, $speed) = split ' ', $1;
			accept my $sock, $lsock or die "accept: $!";
			deb "accept on $lpath =", $sock->fileno;
			local_attach($sock->fileno, $bus, $dev, $speed);
		}elsif(/^-Eof$/){
			last
		}
	}
	undef $$defer;
	$pid
}
sub mk_uevent_socket {
	use constant { AF_NETLINK => 16, NETLINK_KOBJECT_UEVENT => 15 };
	socket my $sock, AF_NETLINK, SOCK_DGRAM, NETLINK_KOBJECT_UEVENT
		or die "socket(kobject_uevent): $!";
	bind $sock, pack 'Sx[SL]L', AF_NETLINK, 1
		or die "bind(kobject_uevent): $!";
	return $sock;
}
sub daemonize {
	use_syslog();
	xopen *STDOUT, '>/dev/null';
	xopen *STDERR, '>&STDOUT';
	die "fork: $!" unless defined(my $pid = fork);
	exit if $pid;
}
sub remote_script {
	my $pat = mk_dev_pattern(shift);
	my %opt = map {$_ => 1} @_;
	STDOUT->autoflush;
	xopen *STDIN, '</dev/null';
	xopen *STDERR, '>&STDOUT';
	$SIG{HUP} = 'IGNORE'; # handle it via POLLHUP below
	my $tmpdir = xtmpdir;
	my $rpath = "$tmpdir/host";
	my ($uevent, %sock);
	use IO::Poll qw(POLLIN POLLERR POLLHUP); sub POLLRDHUP(){ 0x2000 }
	my $poll = new IO::Poll;
	my @dev = find_dev($pat, $opt{vhub} || (1, 1));
	print "-Socket $rpath\n";
	for(my $d = .0001; $d < 2; $d *= 2){
		last if -S $rpath;
		select undef, undef, undef, $d;
	}
	my $new_dev = sub {
		my ($busid, $bus, $dev, $speed) = @_;
		print "-Dev $bus $dev $speed\n";
		my $sock = mk_unix_socket c => $rpath;
		deb "connect to $rpath =", $sock->fileno;
		remote_attach($sock->fileno, $busid, !$opt{dounmount});
		unless($opt{nolinger}){
			$poll->mask($sock, POLLRDHUP);
			$sock{$sock} = $busid;
		}
	};
	for my $busid (@dev){
		my ($bus, $dev, $speed, $class) =
			map xreadfile("$devices/$busid/$_"),
			qw(busnum devnum speed bDeviceClass);
		next if $class eq '09';	# XXX a hub
		$new_dev->($busid, $bus, $dev, $speed);
	};
	unless($opt{vhub}){
		print "-Eof\n";
		undef $tmpdir;
		daemonize();
	}else{
		$poll->mask($uevent = mk_uevent_socket, POLLIN);
		$poll->mask(*STDOUT, POLLERR);
		# POLLERR because 0 would instead remove the fd in perl
		# nb: this only works on Linux; POLLIN would be better but that
		# may cause it to return it as readable when stdin == stdout
	}
	while($uevent or keys %sock){
		if($poll->poll == -1){
			next if $!{EINTR}; die "poll: $!";
		}
		if($poll->events(*STDOUT)){
			daemonize();
			$poll->mask($_, 0) for *STDOUT, $uevent;
			undef $uevent;
		}
		for($poll->handles(POLLRDHUP)){
			xeval { remote_detach(delete $sock{$_}) };
			$poll->mask($_, 0);
		}
		if($poll->handles(POLLIN)){
			if(recv $uevent, my $d, 65536, MSG_DONTWAIT){
				my ($t, %e) = map split('='), split "\0", $d;
				next unless
					$e{ACTION} eq 'bind' &&
					$e{DEVTYPE} eq 'usb_device' &&
					$e{DRIVER} eq 'usb' &&
					$e{TYPE} !~ '^9/';	# XXX a hub
				my $p = "$SYSFS$e{DEVPATH}";
				my $busid = $p =~ s{.*/}{}r;
				my $spec = mk_dev_spec($p, %e);
				deb "UEVENT $spec";
				next unless $spec =~ $pat;
				$new_dev->($busid, $e{BUSNUM}, $e{DEVNUM},
					xreadfile("$p/speed"));
			}elsif(not $!{ENOBUFS} || $!{EINTR} || $!{EWOULDBLOCK}){
				die "sysread(kobject_uevent): $!";
			}
		}
	}
}
sub dv { $_[0] =~ y/:./.:/r =~ s/(\d+)/pack"N",$1/ger }
sub nsort { sort {dv($a) cmp dv($b)} @_ }
sub find {
	my ($d, $pat, $cb) = @_;
	for(do { my $h; opendir $h, $d and readdir $h }){
		next if /^\.\.?$/ or -l (my $s = "$d/$_");
		find($s, $pat, $cb) if -d _;
		$cb->($s) if /\A$pat\z/;
	}
}
sub list_devices {
	my $pat = mk_dev_pattern(shift);
	my %opt = map {$_ => 1} @_;
	my %c = (
		'01' => 'audio', '02' => 'COM',
		'03' => 'HID',
			'030001' => 'kbd',
			'030002' => 'mouse',
			'030101' => 'kbd',	# boot
			'030102' => 'mouse',	# boot
		'07' => 'printer', '08' => 'UMASS',
		'09' => 'hub', '0a' => 'CDC', '0e' => 'video',
		'e0' => 'wireless',
			'e00101' => 'bluetooth',
			'e00103' => 'RNDIS',
		# 'ff' vendor
			'ff4201' => 'adb',
	);
	opendir my $d, $devices or die "opendir $devices: $!";
	for(nsort readdir $d){
		my ($busid) = /^([1-9][\d.-]+)$/ or next;
		my $path = "$devices/$_";
		my %e = read_uevent "$path/uevent";
		my $indent = $busid =~ y/.// * 2;
		my $spec = mk_dev_spec($path, %e);
		next unless $spec =~ $pat;
		printf "%*s%s", $indent, '', $spec =~ s/\t.*//r;
		print $e{DRIVER} eq 'usb' ? "\n" : "  [$e{DRIVER}]\n";
		for my $if (<$path/$busid:*>){
			my %e = read_uevent "$if/uevent";
			my ($c, $s, $p) = map sprintf('%02x', $_),
				split '/', $e{INTERFACE};
			next if $c eq '09';	# skip hub if
			my (@extra);
			my $ifs = $c{$c.$s.$p} // $c{$c.$s} // $c{$c} //
				readfile("$if/interface") =~ s#(\S+).*#/$1#r;
			if(my $drv = $e{DRIVER}){
				my @d;
				find $if, 'uevent', sub {
					return unless my %u = read_uevent shift;
					push @d, $u{DEVNAME} =~ s{^input/}{}r
						if exists $u{DEVNAME};
					push @d, ":$u{RFKILL_NAME}"
						if exists $u{RFKILL_NAME};
					push @d, ":$u{INTERFACE}"
						if $u{INTERFACE} =~ m#[^\d/]#;
				};
				push @extra, "[$drv]", nsort @d;
			}
			printf "%*s    %s %s%s%s %-7s %s\n", $indent, '',
				$if =~ s/.*:/:/r,
				$c, $s, $p, $ifs, join ' ', @extra;
		}
	}
}
sub persistent {
	my @args = @_;
	unshift @args, qw(
		-oConnectTimeout=15 -oServerAliveInterval=15
		-oCheckHostIP=no -oBatchMode=yes);
	my ($minwait, $maxwait) = (.5, 60);
	for(my $wait = $minwait;;){
		my $ot = time;
		xeval { xwait local_script(@args) };
		my $dt = time - $ot;
		deb "done after $dt seconds";
		if($dt < $wait){
			my $st = $wait / ($dt > 1 ? $dt : 1);
			deb "will sleep for $st seconds";
			sleep $st;
		}
		if($dt > $maxwait){
			$wait = $minwait;
		}elsif(($wait *= 4) > $maxwait){
			$wait = $maxwait;
		}
	}
}
sub use_syslog {
	my $sock = mk_unix_socket d => "/dev/log";
	$logger = sub {
		syswrite $sock, sprintf '<%d>%s %s: %s',
			LOG_DAEMON|shift, substr(localtime, 4, 15),
			$id, "@_" =~ s/\s+$//r;
	};
}
sub daemon {
	use POSIX qw(setsid);
	return if my $pid = fork;
	die "fork: $!" unless defined $pid;
	use_syslog();
	die "setsid: $!" unless setsid;
	xopen *STDIN, '+</dev/null';
	xopen $_, '>&STDIN' for *STDOUT, *STDERR;
	unshift @_, '-y';	# let ssh use syslog
	persistent(@_);
}
__EOT__
